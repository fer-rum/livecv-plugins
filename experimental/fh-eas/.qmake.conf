
# Configuration paths for Live CV
#   LIVECV_BIN_PATH -
#      The path where Live CV binaries reside.
#      In case you are building Live CV from source, together with this project, then you
#      can configure it as $$shadowed($$PWD)/dependencies/livecv/bin, which is equivalent
#      to the $$OUT_PWD (or output directory) of this project), concatenated with the
#      dependency subproject
#
#   LIVECV_DEV_PATH -
#      The path to Live CV dev directory.
#      In case Live CV is build from source, this is basically Live CVs source directory
#      (required for 'include' and 'qmake project' files.
#
#    PROJECT_ROOT -
#      The path to the source code root of this project
#
#    BUILD_DEPENDENCIES -
#      In case you are using a source from Live CV, you need to build it first
#
# Note: The deployment and output paths of this project are configured automatically in the
# 'package.pri' file included from Live CV's dev dir

TEMP = $$(LIVECV_BIN_PATH)  # cache the environment variable into a local one
clear(LIVECV_BIN_PATH)
!isEmpty(TEMP){             # since 'isEmpty' will not work correctly otherwise
    LIVECV_BIN_PATH = $$(LIVECV_BIN_PATH)
} else {
    LIVECV_BIN_PATH = $$shadowed($$PWD)/dependencies/livecv/bin
}
debug(LiveCV bin path is $$LIVECV_BIN_PATH, 1)

TEMP = $$(LIVECV_BIN_PATH)  # cache the environment variable into a local one
clear(LIVECV_DEV_PATH)
!isEmpty(TEMP){             # since 'isEmpty' will not work correctly otherwise
    LIVECV_DEV_PATH = $$(LIVECV_DEV_PATH)
} else {
    LIVECV_DEV_PATH = $$PWD/dependencies/livecv
}
debug(LiveCV dev path is $$LIVECV_BIN_PATH, 1)

PROJECT_ROOT = $$PWD

BUILD_DEPENDENCIES = true

exists($$PWD/config.pri):include($$PWD/config.pri)
include($$LIVECV_DEV_PATH/project/package.pri)
